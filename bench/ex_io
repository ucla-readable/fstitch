#!/usr/bin/env perl

use File::Basename;
use Getopt::Std;

sub usage() {
	$basename = basename($0);
	print STDERR "Usage: $basename [-h] <-d TEST_DIR> <-t TEST_NAME>\n";
	exit(1);
}

my %opt;

getopts("hd:t:", \%opt) or usage();
usage() if $opt{h} || !defined $opt{d} || !defined $opt{t};

if (!-d "$opt{d}") {
	print STDERR "Test output directory does not exist ($opt{d})\n";
	exit(1);
}

my $testdir = $opt{d};
my $testname = $opt{t};

if (!-f "$testdir/$testname.run.0") {
	print STDERR "No $testname runs\n";
	exit(1);
}

my $havedata = 0;
my %writes_dispatched = ("sum" => 0, "count" => 0, "min" => -1, "max" => 0);
my %writes_size = %writes_dispatched;
my %writes_multiblocks = %writes_dispatched;
my %writes_multiwrites = %writes_dispatched;
my %reads_dispatched = %writes_dispatched;
my %reads_size = %writes_dispatched;

sub addtest {
	my $table = shift;
	my $val = shift;
	$val =~ tr/,//d; # some locales insert commas. human readability? crazy.
	$table->{"sum"} += $val;
	$table->{"count"}++;
	$table->{"min"} = $val if ($val < $table->{"min"} || $table->{"min"} == -1);
	$table->{"max"} = $val if ($val > $table->{"max"});
}

sub printstats {
	my $stream = shift;
	my $table = shift;
	my $avg = 0;
	$avg = $table->{"sum"} / $table->{"count"} if ($table->{"count"} != 0);
	printf $stream "%.3f %.3f %.3f  ", $avg, $table->{"min"}, $table->{"max"};
}

my $cachefile = "$testdir/.$testname.blk.ex_cache";

sub printcache {
	open(C, "<$cachefile") || die "cannot open <$cachefile: $!";
	while(<C>) {
		print "$_";
	}
	close(C);
}

if (-r $cachefile) {
	printcache();
	exit(0);
}

foreach $blk (<$testdir/$testname.blk.*>) {
	# remove blktrace suffix for blkparse
	$blk =~ m/(.*\.blk\.\d+)\./;
	$blk = $1;

	my $writes_dispatched = -1;
	my $writes_size = -1;
	my %writes_blocks = ();
	my $reads_dispatched = -1;
	my $reads_size = -1;
	open(R, "blkparse -i $blk |") || die;
	while(<R>) {
		if (m/Write Dispatches:\s+(\d+(,\d+){0,1}),\s+(\d+(,\d+){0,1})KiB/) {
			$havedata = 1;
			addtest(\%writes_dispatched, $1);
			addtest(\%writes_size, $3);
		}
		if (m/Read Dispatches:\s+(\d+(,\d+){0,1}),\s+(\d+(,\d+){0,1})KiB/) {
			addtest(\%reads_dispatched, $1);
			addtest(\%reads_size, $3);
		}
		if (m/^\s*\d+,\d+\s+\d+\s+(\d+)\s+(\d+\.\d+)\s+\d+\s+([CDIQWBFMGSPUTXA]{1,2})\s+([RAWBS]{1,3})(.*)$/) {
			my $seq = $1;
			my $ts = $2;
			my $action = $3;
			my $rwbs = $4;
			my $action_detail = $5;
			if ($action =~ m/D/ && $rwbs =~ m/W/) {
				$action_detail =~ m/^\s+(\d+) \+ (\d+)/ || die;
				my $blockno = $1;
				my $nblocks = $2;
				for (my $b = $blockno; $b < $blockno + $nblocks; $b++) {
					if (! exists $writes_blocks{$b}) {
						$writes_blocks{$b} = 1;
					} else {
						$writes_blocks{$b}++;
					}
				}
			}
		}
	}
	close(R);

	my $nmultiblocks = 0;
	my $nmultiwrites = 0;
	while (my ($blockno, $nwrites) = each(%writes_blocks)) {
		if ($nwrites > 1) {
			$nmultiblocks++;
			$nmultiwrites += $nwrites;
		}
	}
	addtest(\%writes_multiblocks, $nmultiblocks);
	addtest(\%writes_multiwrites, $nmultiwrites);
}

exit if !$havedata;

open(C, ">$cachefile") || die "cannot open >$cachefile: $!";
printstats(\*C, \%writes_dispatched);
printstats(\*C, \%writes_size);
printstats(\*C, \%writes_multiblocks);
printstats(\*C, \%writes_multiwrites);
printstats(\*C, \%reads_dispatched);
printstats(\*C, \%reads_size);
print C "\n";
close(C);

printcache();
